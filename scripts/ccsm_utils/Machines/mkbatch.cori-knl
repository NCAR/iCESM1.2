#! /bin/csh -f

#################################################################################
if ($PHASE == set_batch) then
#################################################################################
cd ${CASEROOT}
source ${CASEROOT}/Tools/ccsm_getenv || exit -1

set ntasks  = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
@ nodes = $ntasks / ${MAX_TASKS_PER_NODE}
if ( $ntasks % ${MAX_TASKS_PER_NODE} > 0) then
  @ nodes = $nodes + 1
  @ ntasks = $nodes * ${MAX_TASKS_PER_NODE}
endif

@ taskpernode = ${MAX_TASKS_PER_NODE} / ${maxthrds}
set qname = regular
set tlimit = "00:59:00"
if ($CCSM_ESTCOST > 2) set tlimit = "04:59:00"

if ($?TESTMODE) then
 set file = $CASEROOT/${CASE}.test 
 set qname = debug
 set tlimit = "30:00"
else
 set file = $CASEROOT/${CASE}.run 
endif

cat >! $file << EOF1
#!/bin/csh -f
#SBATCH --job-name=${CASE}
#SBATCH --time=$tlimit
#SBATCH --nodes=$nodes
##SBATCH --ntasks=$ntasks
##SBATCH --cpus-per-task=$maxthrds
#SBATCH --exclusive
###SBATCH --mail-type=END
###SBATCH --mail-user=xyz@abc.com
#SBATCH --output=cesm%jodid.out
#SBATCH --error=cesm%jodid.err
#SBATCH --license SCRATCH,project
#SBATCH -p $qname
#SBATCH -C knl
EOF1

#################################################################################
else if ($PHASE == set_exe) then
#################################################################################
cd ${CASEROOT}
source ${CASEROOT}/Tools/ccsm_getenv || exit -1

set ntasks  = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
@ nodes = $ntasks / ${MAX_TASKS_PER_NODE}
if ( $ntasks % ${MAX_TASKS_PER_NODE} > 0) then
  @ nodes = $nodes + 1
  @ ntasks = $nodes * ${MAX_TASKS_PER_NODE}
endif

set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
set maxtasks = `${CASEROOT}/Tools/taskmaker.pl -sumtasks`
@ aaa = ( 256 * $nodes ) / $maxtasks
cat >> ${CASEROOT}/${CASE}.run << EOF1
# -------------------------------------------------------------------------
# Run the model
# -------------------------------------------------------------------------

sleep 2
cd \$RUNDIR
echo "\`date\` -- CSM EXECUTION BEGINS HERE" 


setenv OMP_STACKSIZE 128M
setenv OMP_NUM_THREADS ${maxthrds}
srun --label -n ${maxtasks} -c ${aaa} --cpu_bind=cores --cpu_bind=verbose --kill-on-bad-exit \$EXEROOT/cesm.exe >&! cesm.log.\$LID

wait
echo "\`date\` -- CSM EXECUTION HAS FINISHED" 

EOF1


#################################################################################
else if ($PHASE == set_larch) then
#################################################################################
set account_name = ` `
if (-e ~/.ccsm_proj) then
   set account_name = `head -1 ~/.ccsm_proj`
endif

cat >! $CASEROOT/${CASE}.l_archive << EOF1
#! /bin/csh -f
#SBATCH --job-name=${CASE}.lta
#SBATCH --time=30:00
#SBATCH --nodes=1
##SBATCH --ntasks=32
#SBATCH --cpus-per-task=1
#SBATCH --exclusive
###SBATCH --mail-type=END
###SBATCH --mail-user=xyz@abc.com
#SBATCH --output=lta%jodid.out
#SBATCH --error=lta%jodid.err
#SBATCH -p regular
cd $CASEROOT 
source $CASEROOT/Tools/ccsm_getenv || exit -1
cd \$DOUT_S_ROOT
$CASEROOT/Tools/lt_archive.sh -m copy_dirs_hsi
exit 0

EOF1
chmod 775 ${CASEROOT}/${CASE}.l_archive

#################################################################################
else
#################################################################################

    echo "  PHASE setting of $PHASE is not an accepted value"
    echo "  accepted values are set_batch, set_exe and set_larch"
    exit 1

#################################################################################
endif
#################################################################################
